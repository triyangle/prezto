export PATH="/usr/local/opt/node@8/bin:$PATH"
export CS61B_LIB_DIR="$HOME/school/CS/61B/CS61B/javalib/*"

function trailing_ws() {
  find . -iname $1 -type f -exec sed -i '' 's/[[:space:]]\{1,\}$//' {} \+
}

alias bt='battery-time'
alias bp='battery-percentage'

alias ssh='TERM=xterm-256color ssh'
alias vagrant='TERM=xterm-256color vagrant'

export CLASSPATH="$CLASSPATH:$CS61B_LIB_DIR:./"

# Source Prezto.
if [[ -t 0 && -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...
export KEYTIMEOUT=1
export HISTCONTROL=ignoreboth

export VIRTUAL_ENV_DISABLE_PROMPT=1

alias tws='trailing_ws'
alias et='expand_tabs'

# fasd settings
alias v='f -e vim' # quick opening files with vim
alias n='f -e nvim' # quick opening files with nvim
eval "$(fasd --init zsh-hook zsh-ccomp zsh-ccomp-install zsh-wcomp zsh-wcomp-install posix-alias posix-hook)"
bindkey '^X^A' fasd-complete    # C-x C-a to do fasd-complete (files and directories)
bindkey '^X^F' fasd-complete-f  # C-x C-f to do fasd-complete-f (only files)
bindkey '^X^D' fasd-complete-d  # C-x C-d to do fasd-complete-d (only directories)

# Autosuggest settings
# export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=10'
export ZSH_AUTOSUGGEST_STRATEGY=(match_prev_cmd history)

bindkey '^P' up-history
bindkey '^N' down-history
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
bindkey '^d' kill-word
bindkey '^r' history-incremental-search-backward
bindkey '^k' kill-line
bindkey '^e' end-of-line
bindkey '^a' beginning-of-line
bindkey '^[b' backward-word
bindkey '^[f' forward-word

# fzf settings
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
# export FZF_TMUX=0
export FZF_DEFAULT_COMMAND='find .'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

__fselhome() {
  setopt localoptions pipefail 2> /dev/null
  eval "$1 | $(__fzfcmd) -m $FZF_CTRL_T_OPTS" | while read item; do
    echo -n "${(q)item} "
  done
  local ret=$?
  echo
  return $ret
}

__fzfcmd() {
  [ ${FZF_TMUX:-1} -eq 1 ] && echo "fzf-tmux -d${FZF_TMUX_HEIGHT:-40%}" || echo "fzf"
}

fzf-file-widgethome() {
  LBUFFER="${LBUFFER}$(__fselhome "find ~")"
  local ret=$?
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  return $ret
}

fzf-file-widgetroot() {
  LBUFFER="${LBUFFER}$(__fselhome "find /")"
  local ret=$?
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  return $ret
}

zle     -N   fzf-file-widgethome
bindkey '^ ' fzf-file-widgethome

zle -N fzf-file-widgetroot
bindkey '^_' fzf-file-widgetroot

function expand_tabs() {
  find . -name $1 ! -type d -exec bash -c 'expand -t 4 "$0" > /tmp/e && mv /tmp/e "$0"' {} \;
}

function chpwd() {
  emulate -L zsh
  k
}

# Uncomment to add newline after every command
# precmd() {
#  print ""
# }

source $HOME/dotfiles/submodules/k/k.sh
source $HOME/dotfiles/submodules/zsh-interactive-cd/zsh-interactive-cd.plugin.zsh

export PATH=/usr/local/anaconda3/bin:"$PATH"

function gi() { curl -sL https://www.gitignore.io/api/$@ ;}

_gitignoreio_get_command_list() {
  curl -sL https://www.gitignore.io/api/list | tr "," "\n"
}

_gitignoreio () {
  compset -P '*,'
  compadd -S '' `_gitignoreio_get_command_list`
}

compdef _gitignoreio gi

_spotify() {
    local curcontext="$curcontext" state line
    typeset -A opt_args

    _arguments \
        '1: :->command'\
        '*:: :->subcmd'

    case $state in
    (command)
        local -a commands
        commands=(
            play:'Play a song, album, artist, list, or uri'
            next:'Skip to next track'
            prev:'Back to prev track'
            replay:'Replay current track'
            pos:'Jump to pos <time>'
            pause:'Pause'
            quit:'Quit'
            vol:'Up, down, set, or show'
            status:'Current track info'
            share:'Get url and/or uri for current track'
            toggle:'Toggle shuffle or repeat'
        )
        _describe -t all_commands "all commands" commands
        ;;
    (subcmd)
        case ${line[1]} in
        (play)
            local -a subcmds
            subcmds=(album artist list uri)
            _describe -t subcmds "play subcommands" subcmds
            ;;
        (vol)
            local -a subcmds
            subcmds=(up down show)
            _describe -t subcmds "vol subcommands" subcmds
            ;;
        (share)
            local -a subcmds
            subcmds=(url uri)
            _describe -t subcmds "share subcommands" subcmds
            ;;
        (toggle)
            local -a subcmds
            subcmds=(shuffle repeat)
            _describe -t subcmds "toggle subcommands" subcmds
            ;;
        esac
    esac
}

compdef _spotify spotify

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
